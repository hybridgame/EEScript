///////////////////////////////////////////////////////////////////////////////
// 적과 마주치면 그전에 하던 모든 행동을 멈추고 파티에 알린다.
///////////////////////////////////////////////////////////////////////////////

// 투명화, 그림자 숨기 중에는 스크립트를 정지한다.
IF
	OR(2)
		StateCheck(Myself,STATE_INVISIBLE)
		ModalState(STEALTH)
THEN
	RESPONSE #100
END

// 적과 마주치면 무기를 착용하고 그전의 모든 행동을 취소한다.
IF
    See(NearestEnemyOf(Myself))
    Global("ES_COMBAT","LOCALS",%COMBAT_OFF%)
    Global("ES_MODE","LOCALS",%MODE_MELEE%)
THEN
	RESPONSE #100
		SetGlobal("ES_COMBAT","LOCALS",%COMBAT_ON%)
		EquipMostDamagingMelee()
        SelectWeaponAbility(SLOT_WEAPON,0)
		ClearActions(Myself)
		Continue()
END

IF
    See(NearestEnemyOf(Myself))
    Global("ES_COMBAT","LOCALS",%COMBAT_OFF%)
    Global("ES_MODE","LOCALS",%MODE_RANGE%)
	CanEquipRanged()
THEN
	RESPONSE #100
		SetGlobal("ES_COMBAT","LOCALS",%COMBAT_ON%)
        EquipRanged()
		ClearActions(Myself)
		Continue()
END

IF
    See(NearestEnemyOf(Myself))
    Global("ES_COMBAT","LOCALS",%COMBAT_OFF%)
	Global("ES_MODE","LOCALS",%MODE_MIXED%)
THEN
	RESPONSE #100
		SetGlobal("ES_COMBAT","LOCALS",%COMBAT_ON%)
		ClearActions(Myself)
		Continue()
END

// 파티에게 적과 조우한 것을 알린다.
IF
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("ES_SHOUT_TIMER","LOCALS")
THEN
	RESPONSE #100
		GlobalShout(%SHOUT_MOVE%)
		SetGlobalTimer("ES_SHOUT_TIMER","LOCALS",TWO_ROUNDS)
		Continue()
END

///////////////////////////////////////////////////////////////////////////////
// 전투중인데 적이 보이지 않으면 적을 보고있는 파티원에게 이동한다.
///////////////////////////////////////////////////////////////////////////////

IF
	Heard([GOODCUTOFF],%SHOUT_MOVE%)
	ActionListEmpty()
	!See(NearestEnemyOf(Myself))
	!CombatCounter(0)
THEN
	RESPONSE #100
		SetGlobal("ES_COMBAT","LOCALS",%COMBAT_OFF%)
		MoveToObject(LastHeardBy(Myself))
END