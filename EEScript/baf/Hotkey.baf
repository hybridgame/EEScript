///////////////////////////////////////////////////////////////////////////////
// 	스크립트 시작시 상태 출력
///////////////////////////////////////////////////////////////////////////////

IF
	OnCreation()
	Global("ES_AI","LOCALS",%AI_ON%)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,%S_AI_ON%)
		Continue()
END

IF
	OnCreation()
	Global("ES_AI","LOCALS",%AI_OFF%)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,%S_AI_OFF%)
		Continue()
END

IF
	OnCreation()
	Global("ES_MODE","LOCALS",%MODE_RANGE%)
	!CanEquipRanged()
THEN
	RESPONSE #100
        DisplayStringHead(Myself,%S_MODE_DISABLE%)
		Continue()
END

///////////////////////////////////////////////////////////////////////////////
// 인공지능을 끄고 키는 핫키
///////////////////////////////////////////////////////////////////////////////

IF
	HotKey(V)
	!Global("ES_AI","LOCALS",%AI_ON%)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,%S_AI_ON%)
		SetGlobal("ES_AI","LOCALS",%AI_ON%)
		Continue()
END

IF
	HotKey(V)
	!Global("ES_AI","LOCALS",%AI_OFF%)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,%S_AI_OFF%)
		SetGlobal("ES_AI","LOCALS",%AI_OFF%)
END

///////////////////////////////////////////////////////////////////////////////
// 인공지능이 꺼져 있으면 스크립트를 중지한다.
///////////////////////////////////////////////////////////////////////////////

IF
	Global("ES_AI","LOCALS",%AI_OFF%)
THEN
	RESPONSE #100
END

///////////////////////////////////////////////////////////////////////////////
// 전투 모드 지정 핫키
///////////////////////////////////////////////////////////////////////////////

IF
	HotKey(N)
	Global("ES_MODE","LOCALS",%MODE_MELEE%)
	CanEquipRanged()
THEN
	RESPONSE #100
        DisplayStringHead(Myself,%S_MODE_RANGE%)
		EquipRanged()
		SetGlobal("ES_MODE","LOCALS",%MODE_RANGE%)
END

IF
	HotKey(N)
	Global("ES_MODE","LOCALS",%MODE_MELEE%)
	!CanEquipRanged()
THEN
	RESPONSE #100
        DisplayStringHead(Myself,%S_MODE_DISABLE%)
		SetGlobal("ES_MODE","LOCALS",%MODE_RANGE%)
END

IF
	HotKey(N)
	Global("ES_MODE","LOCALS",%MODE_RANGE%)
THEN
	RESPONSE #100
        DisplayStringHead(Myself,%S_MODE_MIXED%)
		SetGlobal("ES_MODE","LOCALS",%MODE_MIXED%)
END

IF
	HotKey(N)
	Global("ES_MODE","LOCALS",%MODE_MIXED%)
THEN
	RESPONSE #100
        DisplayStringHead(Myself,%S_MODE_MELEE%)
		SelectWeaponAbility(SLOT_WEAPON,1)
		SetGlobal("ES_MODE","LOCALS",%MODE_MELEE%)
END

///////////////////////////////////////////////////////////////////////////////
// 해야할 행동 지정 핫키
///////////////////////////////////////////////////////////////////////////////

// 모든 시프, 몽크
IF
	HotKey(B)
	OR(2)
		Class(Myself,THIEF_ALL)
		Class(Myself,MONK)
	Global("ES_ACT","LOCALS",%ACT_FIND%)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,%S_ACT_HIDE%)
		SetGlobal("ES_ACT","LOCALS",%ACT_HIDE%)
END

IF
	HotKey(B)
	Class(Myself,THIEF_ALL)
	Global("ES_ACT","LOCALS",%ACT_HIDE%)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,%S_ACT_DETECT%)
		SetGlobal("ES_ACT","LOCALS",%ACT_DETECT%)
END

IF
	HotKey(B)
	Class(Myself,CLERIC_THIEF)
	!ButtonDisabled(BUTTON_TURNUNDEAD)
	Global("ES_ACT","LOCALS",%ACT_DETECT%)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,%S_ACT_TURN%)
		SetGlobal("ES_ACT","LOCALS",%ACT_TURN%)
END

// 모든 레인저
IF
	HotKey(B)
	Class(Myself,RANGER_ALL)
	Global("ES_ACT","LOCALS",%ACT_NONE%)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,%S_ACT_HIDE%)
		SetGlobal("ES_ACT","LOCALS",%ACT_HIDE%)
END

IF
	HotKey(B)
	Class(Myself,CLERIC_RANGER)
	!ButtonDisabled(BUTTON_TURNUNDEAD)
	Global("ES_ACT","LOCALS",%ACT_HIDE%)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,%S_ACT_TURN%)
		SetGlobal("ES_ACT","LOCALS",%ACT_TURN%)
END

// 팔라딘, 모든 클레릭
IF
	HotKey(B)
	OR(2)
		Class(Myself,PALADIN_ALL)
		Class(Myself,CLERIC_ALL)
	!ButtonDisabled(BUTTON_TURNUNDEAD)
	Global("ES_ACT","LOCALS",%ACT_NONE%)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,%S_ACT_TURN%)
		SetGlobal("ES_ACT","LOCALS",%ACT_TURN%)
END

// 바드
IF
	HotKey(B)
	Class(Myself,BARD_ALL)
	!ButtonDisabled(BUTTON_BATTLESONG)
	Global("ES_ACT","LOCALS",%ACT_NONE%)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,%S_ACT_SONG%)
		SetGlobal("ES_ACT","LOCALS",%ACT_SONG%)
END

// 샤먼
IF
	HotKey(B)
	Class(Myself,SHAMAN)
	!ButtonDisabled(BUTTON_BATTLESONG)
	Global("ES_ACT","LOCALS",%ACT_FIND%)
THEN
	RESPONSE #100
		// 샤먼의 춤
		SetGlobal("ES_ACT","LOCALS",%ACT_DANCE%)
END

IF
	HotKey(B)
	Class(Myself,SHAMAN)
	Global("ES_ACT","LOCALS",%ACT_DANCE%)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,%S_ACT_DETECT%)
		SetGlobal("ES_ACT","LOCALS",%ACT_DETECT%)
END

// 기본 행동
IF
	HotKey(B)
	OR(3)
		Class(Myself,THIEF_ALL)
		Class(Myself,MONK)
		Class(Myself,SHAMAN)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,%S_ACT_FIND%)
		SetGlobal("ES_ACT","LOCALS",%ACT_FIND%)
END

IF
	HotKey(B)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,%S_ACT_NONE%)
		SetGlobal("ES_ACT","LOCALS",%ACT_NONE%)
END